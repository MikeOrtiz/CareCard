configurations {
    liquibase
}

dependencies {
    liquibase group: 'org.liquibase.ext', name: 'liquibase-hibernate5', version: 3.8
    liquibase "org.liquibase:liquibase-core:3.8.6"
}

task liquibaseDiffChangelog(dependsOn: compileJava, type: JavaExec) {
    group = "liquibase"

    classpath sourceSets.main.runtimeClasspath
    classpath configurations.liquibase

    main = "liquibase.integration.commandline.Main"

    args "--changeLogFile=src/main/resources/db/changelog/" + buildTimestamp() +"_changelog.xml"
    args "--username=sa"
    args "--password="
    args "--url=jdbc:h2:mem:testdb?dialect=org.hibernate.dialect.H2Dialect"
    args "--driver=org.h2.Driver"
    args "--logLevel=debug"
    args "--referenceUrl=hibernate:spring:com.coronacarecard.dao.entity?dialect=org.hibernate.dialect.H2Dialect&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy"
    args "--referenceDriver=liquibase.ext.hibernate.database.connection.HibernateDriver"
    args "diffChangeLog"
}

def buildTimestamp() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}